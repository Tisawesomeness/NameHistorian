plugins {
    id 'io.freefair.lombok' version '6.5.0.3' apply false
}

allprojects {
    group = 'com.tisawesomeness'
    version = '0.3.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'

    dependencies {
        implementation 'com.google.code.findbugs:jsr305:3.0.2'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
        testImplementation 'org.assertj:assertj-core:3.23.1'
    }

    test {
        useJUnitPlatform()
    }

    java {
        sourceCompatibility = '1.8'
        compileJava.options.encoding = 'UTF-8'
        compileTestJava.options.encoding = "UTF-8"
    }
}

def outputTasks() {
    [":nh-spigot:shadowJar"].stream().map({ tasks.findByPath(it) })
}

task copyToOutput(type: Copy) {
    outputTasks().forEach {
        from(it)
    }

    rename '(.*)-all.jar', '$1.jar'

    into 'build/libs'
}

task cleanOutput() {
    delete fileTree("build/libs/").matching {
        include "*.jar"
    }
}

task clean() {
    dependsOn cleanOutput
}

task build() {
    dependsOn copyToOutput
}
